apply from: "config.gradle"

group rootProject.ext.app.group
version rootProject.ext.app.version

buildscript {
    ext {
        springVersion = "4.3.9.RELEASE"
        springBootVersion = "1.5.4.RELEASE"
        springCloudVersion = "1.3.1.RELEASE"
        springCloudConsulVersion = "1.2.1.RELEASE"
        springCloudServicesVersion = "1.5.0.RELEASE"
    }
    repositories {
        mavenCentral()
        maven { url "https://repo.spring.io/plugins-release" }
    }
    dependencies {
        classpath "io.spring.gradle:dependency-management-plugin:1.0.3.RELEASE"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
    }
}

subprojects {
    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    configurations {
        all*.exclude group: 'commons-logging', module: 'commons-logging'
        all*.exclude group: 'org.springframework.boot', module: 'spring-boot-starter-tomcat'

        provided
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://repo.spring.io/release" }
        maven { url "https://repo.spring.io/libs-release" }
    }

    // https://docs.gradle.org/current/userguide/artifact_dependencies_tutorial.html
    // https://docs.gradle.org/current/userguide/dependency_management.html
    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:$springBootVersion"
            mavenBom "org.springframework.boot:spring-boot-starter-parent:$springBootVersion"
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:Dalston.SR1"
            mavenBom "org.springframework.cloud:spring-cloud-consul-dependencies:$springCloudConsulVersion"
            mavenBom "io.pivotal.spring.cloud:spring-cloud-services-dependencies:$springCloudServicesVersion"
        }
    }

    dependencies {
        compile rootProject.ext.dependencies['lombok'],
                rootProject.ext.dependencies['spring-core'],
                rootProject.ext.dependencies['spring-context'],
                rootProject.ext.dependencies['spring-beans']
    }
}
